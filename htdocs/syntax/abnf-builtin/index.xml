<!DOCTYPE html>
<html xmlns="http://www.w3.org/1999/xhtml" xmlns:w="tag:fullstack.wiki,2018:ns/" lang="en" dir="ltr">
	<head>
		<meta charset="UTF-8" />
		<title>RFC5234: ABNF core grammar rules</title>
		<meta name="description" content="Lists the rules available to ABNF documents by default" />
		<link rel="up" href="../index.xml" />
		<link rel="stylesheet" href="/+/highlight.js/solarized-light.css" />
	</head>
	<body>
		<main>

<h1>RFC4234: ABNF core grammar rules</h1>

<p>Previous Versions</p>
<ul>
	<li><a href="https://tools.ietf.org/html/rfc4234">RFC 4234: Augmented BNF for Syntax Specifications: ABNF</a></li>
</ul>

<h2>ABNF Builtin Grammar Rules</h2>

<pre id="rules-abnf" type="abnf" w:space="indent"><code>
ALPHA          =  %x41-5A / %x61-7A   ; A-Z / a-z

BIT            =  "0" / "1"

CHAR           =  %x01-7F
					; any 7-bit US-ASCII character,
					;  excluding NUL

CR             =  %x0D
					; carriage return

CRLF           =  CR LF
					; Internet standard newline

CTL            =  %x00-1F / %x7F
								; controls

DIGIT          =  %x30-39
						; 0-9

DQUOTE         =  %x22
					; " (Double Quote)

HEXDIG         =  DIGIT / "A" / "B" / "C" / "D" / "E" / "F"

HTAB           =  %x09
					; horizontal tab

LF             =  %x0A
					; linefeed

LWSP           =  *(WSP / CRLF WSP)
					; Use of this linear-white-space rule
					;  permits lines containing only white
					;  space that are no longer legal in
					;  mail headers and have caused
					;  interoperability problems in other
					;  contexts.
					; Do not use when defining mail
					;  headers and use with caution in
					;  other contexts.

OCTET          =  %x00-FF
					; 8 bits of data

SP             =  %x20

VCHAR          =  %x21-7E
					; visible (printing) characters

WSP            =  SP / HTAB
					; white space
</code></pre>


				</main>
			</div>
		</div>
	</body>
</html>
